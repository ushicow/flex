*
* DISK I/O DRIVER PACKAGE 
*
* THIS VERSION IS FOR IBS AP10, APPLE II 6809 CARD
*
* NOV.20,2011 NANJA.INFO
*
	.IN APPLE.EQU

	.OR $DE00
	.TF DRIVER.5E00
*
RDMSK	.EQ $1C		READ ERROR MASK
WTMSK	.EQ $5C		WRITE ERROR MASK
RDCMND	.EQ 1		READ COMMAND
WTCMND	.EQ 2		WRITE COMMAND
*
*
* DISK DRIVER ROUTINE JUMP TABLE
*
DREAD	JMP READ	READ A SINGLE SECTOR
DWRITE	JMP WRITE	WRITE A SINGLE SECTOR
DVERFY	JMP VERIFY	VERIFY WRITE OPERATION	
RESTOR	JMP RST		RESTORE HEAD TO TRACK 00
DRIVE	JMP DRV		DRIVE SELECTION
DCHECK	JMP CHKRDY	CHECK READY
DQUICK	JMP CHKRDY	QUICK CHECK READY
DINIT	JMP INIT	DRIVER INITIALIZE
DWARM	JMP WARM	WARM START ROUTINE
DSEEK	JMP SEEK	SEEK ROUTINE
*
* DRIVER INITIALIZATION
*
INIT	LDA #1
	STA DRVNUM	SET APPLE DRIVE #1
	STA TRKNUM	SET APPLE TRACK #1
	CLR SECNUM	SET APPLE SECTOR #0
WARM	RTS		WARM START NOT NEEDED
*
* READ ONE SECTOR
*
READ	BSR SEEK
	BNE R1		SEEK ERROR
	LDA #RDCMND	RWTS READ COMMAND CODE
	STA COMMAND
	BSR A2DOS
R1	BITB #RDMSK
	BNE R2		READ ERROR
	PSHS Y
	LDY #FILEBUF	COPY SECTOR DATA
	LDB #0
R3	LDA ,Y+
	STA ,X+
	INCB
	BNE R3
	PULS Y
	CLRB
R2	RTS
*
* WRITE ONE SECTOR
*
WRITE	BSR SEEK
	BNE W1		SEEK ERROR
	PSHS Y
	LDY #FILEBUF	COPY SECTOR DATA
	LDB #0
W2	LDA ,X+
	STA ,Y+
	INCB
	BNE W2
	PULS Y
	LDA #WTCMND	RWTS WRITE COMMAND CODE
	STA COMMAND
	BSR A2DOS
W1	BITB #WTMSK
	RTS
*
* SEEK THE SPECIFIED TRACK
*
SEEK	INCA		SKIP A TRACK FOR AP2FLEX
	STA TRKNUM
	CMPA #34	MAX TRACK
	BGT ERROR
	DECB
	STB SECNUM
	CMPB #15	MAX SECTOR
	BHI ERROR
	PSHS X
	LDX #SECMAP	POINT TO CORRECT MAP
	LDB B,X
	PULS X
	STB SECNUM
	CLRB
	RTS
ERROR	LDB #$10	SEEK ERROR
	RTS
*
* CALL APPLE DOS RWTS
*
A2DOS	LDA #A.RWTS
	STA FLAG
IDLE	EXG D,D		IDLE
	TST FLAG
	BNE IDLE	NOT READY YET
	CLRB
	LDA ERRCODE
	BEQ L0		NO ERROR
	BITA #$10	CHECK WRITE PROTECT
	BEQ L1
	ORB #$40	SET WRITE PROTECT ERROR
L1	BITA #$20	CHECK VOLUME NUMBER ERROR
	BEQ L2
	ORB #$10	SET NOT FOUND ERROR
L2	BITA #$40	CHECK DRIVE ERROR
	BEQ L3
	ORB #$8		SET CRC ERROR
L3	BITA #$80	CHECK READ ERROR
	BEQ L4
	ORB #$4		SET LOST DATA
L4	BITA #$0F	CHECK OTHER ERROR
	BEQ L0
	ORB #$1C	SET ALL ERROR
L0	RTS
*
* VERIFY LAST SECTOR WRITTEN 
* 
VERIFY	CLRB
	RTS
*
* RESTORE RESTORES THE HEAD TO 00
*
RST	BSR DRV
	BNE D0		DRIVE ERROR
	LDA #0
	LDB #1
	BRA SEEK
D0	RTS
*
* SELECT THE SPECIFIED DRIVE
*
DRV	LDA 3,X		GET DRIVE NUMBER
	CMPA #1		ENSURE IT'S 0 or 1
	BLS DRV2	BRANCH IF OK
	LDB #$0F	ELSE SET ERROR VALUE
	ORCC #1		SET CARRY FLAG
	RTS
DRV2	INCA		APPLE II DRIVE NUM IS 
	STA DRVNUM	  FLEX DRIVE NUM + 1
	BRA OK
*
CHKRDY	LDA 3,X		GET DRIVE NUMBER
	CMPA #1		ENSURE IT'S 0 or 1
	BLS OK		BRANCH IF OK
	LDB #$80	ELSE, SHOW NOT READY
	ORCC #1		SET CARRY FLAG
	RTS		RETURN
OK	CLRB		SHOW NO ERROR
	ANDCC #$FE	CREAR CARRY FLAG
	RTS
*
SECMAP	.HS	00060C03090F0E050B0208070D040A01
*
ZZZZ	.EN
