*
* DISK I/O DRIVER PACKAGE 
*
* THIS VERSION IS FOR IBS AP10, APPLE II 6809 CARD
*
* NOV.20,2011 NANJA.INFO
*
	.IN APPLE.EQU
*
	.OR $DE00
	.TF DRIVER.5E00
*
RDMSK	.EQ $1C		READ ERROR MASK
WTMSK	.EQ $5C		WRITE ERROR MASK
RDCMND	.EQ 1		READ COMMAND
WTCMND	.EQ 2		WRITE COMMAND
IOB	.EQ $77E8+$8000	RWTS IOB
SECBUF	.EQ IOB+8	RWTS DATA BUFFER
*
*
* DISK DRIVER ROUTINE JUMP TABLE
*
DREAD	JMP READ	READ A SINGLE SECTOR
DWRITE	JMP WRITE	WRITE A SINGLE SECTOR
DVERFY	JMP OK		VERIFY WRITE OPERATION	
RESTOR	JMP RST		RESTORE HEAD TO TRACK 00
DRIVE	JMP DRV		DRIVE SELECTION
DCHECK	JMP OK		CHECK READY
DQUICK	JMP OK		QUICK CHECK READY
DINIT	JMP INIT	DRIVER INITIALIZE
DWARM	JMP WARM	WARM START ROUTINE
DSEEK	JMP SEEK	SEEK ROUTINE
*
* DRIVER INITIALIZATION
*
INIT	LDA #1
	STA DRVNUM	SET APPLE DRIVE #1
	STA TRKNUM	SET APPLE TRACK #1
	CLR SECNUM	SET APPLE SECTOR #0
WARM	RTS		WARM START NOT NEEDED
*
* SEEK THE SPECIFIED TRACK
*
SEEK	INCA		SKIP A TRACK FOR AP2FLEX
	STA TRKNUM
	CMPA #34	MAX TRACK
	BGT ERROR
	DECB
	STB SECNUM
	CMPB #15	MAX SECTOR
	BHI ERROR
	PSHS X
	LDX #SECMAP	POINT TO CORRECT MAP
	LDB B,X
	STB SECNUM
	PULS X
	TST FLIP
	BEQ OK
	EXG X,D
	STB SECBUF
	EORA #$80
	STA SECBUF+1
	BRA OK
ERROR	LDB #$10	SEEK ERROR
	RTS
*
* SELECT THE SPECIFIED DRIVE
*
DRV	LDA 3,X		GET DRIVE NUMBER
	CMPA #1		ENSURE IT'S 0 or 1
	BLS DRV2	BRANCH IF OK
	LDB #$0F	ELSE SET ERROR VALUE
	ORCC #1		SET CARRY FLAG
	RTS
DRV2	INCA		APPLE II DRIVE NUM IS 
	STA DRVNUM	  FLEX DRIVE NUM + 1
	BRA OK
*
* RESTORE RESTORES THE HEAD TO 00
*
RST	BSR DRV
	BNE D0		DRIVE ERROR
	CLRA
	LDB #1
	BRA SEEK
D0	RTS
*
* READ ONE SECTOR
*
READ	BSR SEEK
	BNE R1		SEEK ERROR
	LDA #RDCMND	RWTS READ COMMAND CODE
	STA COMMAND
	BSR A2DOS
R1	BITB #RDMSK
	BNE R2		READ ERROR
	TST FLIP
	BNE OK
	PSHS Y
	LDY #FILEBUF	COPY SECTOR DATA
	CLRB
R3	LDA ,Y+
	STA ,X+
	INCB
	BNE R3
	PULS Y
OK	CLRB
R2	RTS
*
* WRITE ONE SECTOR
*
WRITE	BSR SEEK
	BNE W1		SEEK ERROR
	TST FLIP
	BNE W3
	PSHS Y
	LDY #FILEBUF	COPY SECTOR DATA
	CLRB
W2	LDA ,X+
	STA ,Y+
	INCB
	BNE W2
	PULS Y
W3	LDA #WTCMND	RWTS WRITE COMMAND CODE
	STA COMMAND
	BSR A2DOS
W1	BITB #WTMSK
	RTS
*
* CALL APPLE DOS RWTS
*
A2DOS	LDA #F.RWTS
	STA FLAG
IDLE	EXG D,D		IDLE
	TST FLAG
	BNE IDLE	NOT READY YET
	CLRB
	LDA ERRCODE
	BEQ L0		NO ERROR
	BITA #$10	CHECK WRITE PROTECT
	BEQ L1
	ORB #$40	SET WRITE PROTECT ERROR
L1	BITA #$20	CHECK VOLUME NUMBER ERROR
	BEQ L2
	ORB #$10	SET NOT FOUND ERROR
L2	BITA #$40	CHECK DRIVE ERROR
	BEQ L3
	ORB #$8		SET CRC ERROR
L3	BITA #$80	CHECK READ ERROR
	BEQ L4
	ORB #$4		SET LOST DATA
L4	BITA #$0F	CHECK OTHER ERROR
	BEQ L0
	ORB #$1C	SET ALL ERROR
L0	RTS
*
SECMAP	.HS	00060C03090F0E050B0208070D040A01
*
ZZZZ	.EN
