*
* FLEX 6809 FOR THE MILL CONSOLE I/O
*
P.KEY	.EQ $8006	FOR KEY INPUT
P.OUT	.EQ $8007	FOR DISPLAY OUT
P.DOS	.EQ $8008	FOR APPLE DOS
P.FLAG	.EQ $8009	FOR MONITOR
F.STOP	.EQ $FF		P.FLAG FOR BREAK
F.IN	.EQ 1		P.FLAG FOR INPUT

WSTART	.EQ $CD03	FLEX WARM START
MEMEND	.EQ $CC2B	

	.OR $D370
	.TF CONSOLE.5370
*
* TERMINAL INITIALZE ROUTINE 
*
INIT	LDA #$3F
	STA MEMEND 
	LDA #$FF
	STA MEMEND+1
	RTS
*
* TERMINAL INPUT CHAR. ROUTINE - NO ECHO
*
INNECH	EXG D,D		WASTE TIME
	LDA P.KEY
	BEQ INNECH	NOTHING
	CLR P.KEY	FOR NEXT KEY
RET	RTS
*
* TERMINAL INPUT CHAR. ROUTINE - W/ ECHO
*
INPUT	LDA #F.IN
	STA P.FLAG
.1	EXG D,D		IDLE
	TST P.FLAG
	BNE .1
	LDA P.KEY
	CLR P.KEY
*
* TERMINAL OUTPUT CHARACTER ROUTINE
*
OUTPUT	STA P.OUT	FOR 6502
.1	EXG D,D		IDLE
	TST P.OUT
	BNE .1		NOT READY YET
	RTS
*
* TERMINAL STATUS CHECK (CHECK FOR CHARACTER HIT)
*
STATUS  PSHS A
	LDA P.KEY
	PULS A
	RTS
*
* IRQ INTERRUPT HANDLER ROUTINE
*
IHND	RTI
*
MON	LDA #F.STOP
	STA P.FLAG
WAIT	EXG D,D
	BRA WAIT
	RTS

*
* CONSOLE I/O DRIVER VECTOR TABLE
*
	.OR $D3E5	TABLE STARTS AT $D3E5
	.TF CONSOLE.53E5
INCHNE	.DA INNECH	INPUT CHARACTER W/O ECHO
IHNDLR	.DA IHND	IRQ INTERRUPT HANDELER
SWIVEC	.DA $0000	SWI3 VECTOR LOCATION
IRQVEC	.DA $0002	IRQ VECTOR LOCATION
TMOFF	.DA RET		TIMER OFF ROUTINE
TMON	.DA RET		TIMER ON ROUTINE
TMINT	.DA RET		TIMER INITIALIZATION
MONITR	.DA MON		MONITOR ENTRY ADDRESS
TINIT	.DA INIT	TERMINAL INITIALIZATION
STAT	.DA STATUS	CHECK TERMINAL STATUS
OUTCH	.DA OUTPUT	OUTPUT CHARACTOR
INCH	.DA INPUT	INPUT CHARACTOR W/ ECHO
	.EN

