*
* CONSOLE I/O DRIVER PACKAGE 
*
* THIS VERSION IS FOR THE MILL, APPLE II 6809 CARD
*
MFLIP	.EQ $8000	6809 ADDRESS
	.IN APPLE.EQU

CSTART  .EQ $CD00	FLEX COLD START
WSTART	.EQ $CD03	FLEX WARM START
RSTVCT	.EQ $FFFE	6809 RESET VECTOR

	.OR $D370
	.TF CONSOLE.5370
*
* TERMINAL INITIALZE ROUTINE 
*
INIT	LDD #CSTART	SET RESET VECTOR
	STA RSTVCT
	LDA STROBE	CLEAR KEY STROBE
	RTS
*
* TERMINAL INPUT CHAR. ROUTINE - NO ECHO
*
INNECH	EXG D,D		IDLE
	LDA KEYLOC
	BEQ INNECH
	CLR KEYLOC
RET	RTS
*
* TERMINAL INPUT CHAR. ROUTINE - W/ ECHO
*
INPUT	LDD #RDKEY
	STD A2SUB
	LDA #F.6502
	STA FLAG
.1	EXG D,D		IDLE
	TST FLAG
	BNE .1
	LDA AREG
	ANDA #$7F
*
* TERMINAL OUTPUT CHARACTER ROUTINE
*
OUTPUT	EXG D,D		IDLE
	TST TVLOC
	BNE OUTPUT	NOT READY YET
	STA TVLOC
	RTS
*
* TERMINAL STATUS CHECK (CHECK FOR CHARACTER HIT)
*
STATUS  PSHS A
	LDA KEYLOC
	PULS A
	RTS
*
* IRQ INTERRUPT HANDLER ROUTINE
*
IHND	RTI
*
MON	LDA #F.STOP
	STA FLAG
WAIT	EXG D,D
	TST FLAG
	BNE WAIT
	JMP WSTART
*
* CONSOLE I/O DRIVER VECTOR TABLE
*
	.OR $D3E5	TABLE STARTS AT $D3E5
	.TF CONSOLE.53E5
INCHNE	.DA INNECH	INPUT CHARACTER W/O ECHO
IHNDLR	.DA IHND	IRQ INTERRUPT HANDELER
SWIVEC	.DA $FFF2	SWI3 VECTOR LOCATION
IRQVEC	.DA $FFF6	IRQ VECTOR LOCATION
TMOFF	.DA RET		TIMER OFF ROUTINE
TMON	.DA RET		TIMER ON ROUTINE
TMINT	.DA RET		TIMER INITIALIZATION
MONITR	.DA MON		MONITOR ENTRY ADDRESS
TINIT	.DA INIT	TERMINAL INITIALIZATION
STAT	.DA STATUS	CHECK TERMINAL STATUS
OUTCH	.DA OUTPUT	OUTPUT CHARACTOR
INCH	.DA INPUT	INPUT CHARACTOR W/ ECHO
	.EN
