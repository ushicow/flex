* QLOAD - QUICK LOADER
*
* COPYRIGHT (C) 1980 BY
* TECHNICAL SYSTEMS CONSULTANTS, INC.
* 111 PROVIDENCE RD. CHAPEL HILL NC 27514
*
* LOAD FLEX FROM DISK ASSUMING THAT THE DISK I/O
* ROUTINES ARE ALREADY IN MEMORY.  ASSUMES FLEX
* BEGINS ON TRACK #1 SECTOR #1.  RETURNS TO
* MONITOR ON COMPLETION. BEGIN EXECUTION BY
* JUMPING TO LOCATION $C000
*
* EQUATES
*
STACK	.EQ $C07F
MONITR	.EQ $D3F3
READ	.EQ $DE00
RESTORE	.EQ $DE09
DRIVE	.EQ $DE0C
SCTBUF	.EQ $C300	DATA SECTOR BUFFER
*
* START OF UTILITY
*
	.OR $C000
	.TF QLOAD
*
QLOAD	BRA LOAD0
	.DA #0
	.DA #0
	.DA #0
TRK	.DA #1		FILE START TRACK
SCT	.DA #1		FILE START SECTOR
DNS	.DA #0		DENSITY FLAG
LADR	.DA 0		LOAD ADDRESS
*
LOAD0	LDS #STACK	SETUP STACK
	LDX #SCTBUF	POINT TO FCB
	CLR 3,X		SET FOR DRIVE 0
	JSR DRIVE	SELECT DRIVE 0
	LDX #SCTBUF
	JSR RESTORE	NOW RESTORE TO TRACK 0
	LDD TRK		SETUP STARTING TRX & SCT
	STD SCTBUF
	LDY #SCTBUF+256
*
* PERFORM ACTUAL FILE LOAD
*
LOAD1	BSR GETCH	GET A CHARACTER
	CMPA #$02	DATA RECORD HEADER?
	BEQ LOAD2	SKIP IF SO
	CMPA #$16	XFR ADDRESS HEADER?
	BNE LOAD1	LOOP IF NEITHER
	BSR GETCH	GET TRANSFER ADDRESS
	BSR GETCH	DISCARD IT
	BRA LOAD1	CONTINUE LOAD
LOAD2	BSR GETCH	GET LOAD ADDRESS
	STA LADR
	BSR GETCH
	STA LADR+1
	BSR GETCH	GET BYTE COUNT
	TFR A,B		PUT IN B
	TSTA
	BEQ LOAD1	LOOP IF COUNT=0
	LDX LADR	GET LOAD ADDRESS IN X
LOAD3	PSHS B,X
	BSR GETCH	GET A DATA CHARACTER
	PULS B,X
	STA ,X+		PUT CHARACTER
	DECB		END OF DATA IN RECORD?
	BNE LOAD3	LOOP IF NOT
	BRA LOAD1	GET ANOTHER RECORD
*
* GET CHARACTER ROUTINE - READ A SECTOR IF NECESSARY
*
GETCH	CMPY #SCTBUF+256	OUT OF DATA?
	BNE GETCH4	GO READ CHARACTER IF NOT
GETCH2	LDX #SCTBUF	POINT TO BUFFER
	LDD 0,X		GET FORWARD LINK
	BEQ GO		IF ZERO, FILE IS LOADED
	JSR READ	READ NEXT SECTOR
	BNE QLOAD	START OVER IF ERROR
	LDY #SCTBUF+4	POINT PAST LINK
GETCH4	LDA ,Y+		ELSE, GET A CHARACTER
	RTS
*
* FILE IS LOADED, RETURN TO MONITOR
*
GO	JMP (MONITR)	JUMP TO MONITOR
*
	.EN
