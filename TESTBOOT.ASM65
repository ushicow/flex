*
* TESTUTIL BOOTSTRAP CODE FOR AP10 6809 CARD
* 6502 CODE
*
* NOV,20,2021 NANJA.INFO
*
	.MA PRINT	>PRINT MESSAGE
	LDY #$FF
:1	INY
	LDA ]1,Y
	ORA #$80
	JSR COUT
	ASL
	BNE :1
	.EM

	.MA COPYMEM	>COPYMEM SOURCE,DESTINATION
	LDY #0
:1	LDA ]1,Y
	STA ]2,Y
	INY
	BNE :1
	.EM

	.IN APPLE.EQU
	.OR LOADER
	.TF TESTBOOT
*
* SERACH AP10 CARD
*
	JSR HOME
	LDX #$10	SLOT #1
NSLOT	LDA #$02
	STA RAM04,X	SET RAM
	LDA #$AA
	STA $E000
	LDA $E000
	CMP #$AA
	BNE FOUND
	TXA		X=X+$10
	CLC
	ADC #$10
	TAX
	CMP #$80
	BNE NSLOT	NEXT SLOT
	>PRINT NOCARD
	RTS		DONE
FOUND	STA HALT,X	SET ROM
	TXA
	STA AP10	STORE AP10 SLOT NUMBER X 16
	LSR		A/2
	LSR		A/4
	LSR		A/8
	LSR		A/16
	CLC
	ADC #'0+$80
	STA SLOTN
	>PRINT MES
	JMP COPYOS
*
CR	.EQ $8D
BELL	.EQ $7
MES	.AS "AP10 CARD IN SLOT #"
SLOTN	.DA #'0
	.DA #CR
	.DA #0
NOCARD	.DA #BELL
	.AS "AP10 CARD NOT FOUND"
	.DA #0
*
COPYOS	LDX AP10
	STA RAMDF,X
	>COPYMEM $5300,$D300	CONSOLE	DRIVER
	>COPYMEM $5E00,$DE00	DISK DRIVER
	>COPYMEM $6200,$E200	TESTUTIL
	LDA #$E2
	STA $FFFE
	LDA #$00
	STA $FFFF
	STA HALT,X
	JMP BIOS
*
ZZZZ	.EN
