*
* DISK I/O DRIVER PACKAGE 
*
* THIS VERSION IS FOR THE MILL, APPLE II 6809 CARD
*
MFLIP	.EQ $8000	6809 ADDRESS
	.IN APPLE.EQU

	.OR $DE00
	.TF DRIVER.5E00
*
RDMSK	.EQ $1C	READ ERROR MASK
WTMSK	.EQ $5C	WRITE ERROR MASK
RDCMND	.EQ 1
WTCMND	.EQ 2
*
*
* DISK DRIVER ROUTINE JUMP TABLE
*
	JMP READ	READ A SINGLE SECTOR
	JMP WRITE	WRITE A SINGLE SECTOR
	JMP VERIFY	VERIFY LAST SECTOR	
	JMP RESTOR	RESTORE HEAD TO TRACK #0
	JMP DRIVE	SELECT THE SPECIFIED DRIVE
	JMP CHKRDY	CHECK FOR DRIVE READY
	JMP QUICK	QUICK CHECK FOR DRIVE READY
	JMP INIT	DRIVER INITIALIZE (COLD START)
	JMP WARM	DRIVER INITIALIZE (WARM START)
	JMP SEEK	SEEK TO SPECIFIED TRACK
*
* DRIVER INITIALIZATION
*
INIT	LDA #1
	STA CURDRV
	STA TRKREG
	CLR SECREG
	CLR VOLREG
WARM	RTS		WARM START NOT NEEDED
*
* READ ONE SECTOR
*
READ	BSR SEEK
	BNE .1		SEEK ERROR
	LDA #RDCMND	RWTS READ COMMAND CODE
	STA COMREG
	BSR A2DOS
.1	BITB #RDMSK
	RTS
*
* WRITE ONE SECTOR
*
WRITE	BSR SEEK
	BNE .2		SEEK ERROR
	LDA #WTCMND	RWTS WRITE COMMAND CODE
	STA COMREG
	BSR A2DOS
.2	BITB #WTMSK
	RTS
*
SEEK	INCA		SKIP A TRACK FOR AP2FLEX
	STA TRKREG
	CMPA #34	MAX TRACK
	BGT ERROR
	DECB
	STB SECREG
	CMPB #15	MAX SECTOR
	BHI ERROR
	PSHS X
	LDX #SECMAP	POINT TO CORRECT MAP
	LDB B,X
	PULS X
	STB SECREG
	CLR VOLREG
	EXG D,X
	STB SECBUF
	EORA #$80
	STA SECBUF+1
	CLRB
	RTS
ERROR	LDB #$10	SEEK ERROR
	RTS
*
A2DOS	LDA #F.RWTS
	STA FLAG
IDLE	EXG D,D		IDLE
	TST FLAG
	BNE IDLE	NOT READY YET
	CLRB
	LDA RETCODE
	BEQ L0		NO ERROR
	BITA #$10	CHECK WRITE PROTECT
	BEQ L1
	ORB #$40	 SET WRITE PROTECT ERROR
L1	BITA #$20	CHECK VOLUME NUMBER ERROR
	BEQ L2
	ORB #$10	 SET NOT FOUND ERROR
L2	BITA #$40	CHECK DRIVE ERROR
	BEQ L3
	ORB #$8		 SET CRC ERROR
L3	BITA #$80	CHECK READ ERROR
	BEQ L4
	ORB #$4		 SET LOST DATA
L4	BITA #$0F	CHECK OTHER ERROR
	BEQ L0
	ORB #$1C	 SET ALL ERROR
L0	RTS
*
VERIFY	CLRB
	RTS
*
* RESTORE RESTORES THE HEAD TO 00
*
RESTOR	BSR DRIVE
	BNE .3		DRIVE ERROR
	LDA #0
	LDB #1
	BRA SEEK
.3	RTS
*
DRIVE	LDA 3,X		GET DRIVE NUMBER
	CMPA #1		ENSURE IT'S 0 or 1
	BLS DRV2	BRANCH IF OK
	LDB #$0F	ELSE SET ERROR VALUE
	ORCC #1		 SET CARRY FLAG
	RTS
DRV2	INCA
	STA CURDRV
	BRA OK
*
QUICK
CHKRDY	LDA 3,X		GET DRIVE NUMBER
	CMPA #1		ENSURE IT'S 0 or 1
	BLS OK		BRANCH IF OK
	LDB #$80	ELSE, SHOW NOT READY
	ORCC #1		 SET CARRY FLAG
	RTS		 RETURN
OK	CLRB		SHOW NO ERROR
	ANDCC #$FE	 CREAR CARRY FLAG
	RTS
*
SECMAP	.HS	00060C03090F0E050B0208070D040A01
*
	.EN
