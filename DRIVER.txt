*
* DISK DRIVER ROUTINE JUMP TABLE
*
	.OR $DE00
	.TA $5E00
	.TF DRIVER.5E00
*
P.DOS	.EQ $8008	FOR APPLE DOS
IOB	.EQ $F7E8	APPLE DOS IOB $77E8
CURDRV	.EQ IOB+2
VOLREG	.EQ IOB+3
TRKREG	.EQ IOB+4
SECREG	.EQ IOB+5
SECBUF	.EQ IOB+8
NUMBUF	.EQ IOB+$B
COMREG	.EQ IOB+$C
RETCODE	.EQ IOB+$D
*
DRQ	.EQ 2	DRQ BIT MASK
BUSY	.EQ 1	BUSY MASK
RDMSK	.EQ $1C	READ ERROR MASK
VERMSK	.EQ $18	VERIFY ERROR MASK
WTMSK	.EQ $5C	WRITE ERROR MASK
RDCMND	.EQ 1
WTCMND	.EQ 2
SKCMND	.EQ 0
*
	JMP READ	READ A SINGLE SECTOR
	JMP WRITE	WRITE A SINGLE SECTOR
	JMP VERIFY	VERIFY LAST SECTOR	
	JMP RESTOR	RESTORE HEAD TO TRACK #0
	JMP DRIVE	SELECT THE SPECIFIED DRIVE
	JMP CHKRDY	CHECK FOR DRIVE READY
	JMP QUICK	QUICK CHECK FOR DRIVE READY
	JMP INIT	DRIVER INITIALIZE (COLD START)
	JMP WARM	DRIVER INITIALIZE (WARM START)
	JMP SEEK	SEEK TO SPECIFIED TRACK
*
* DRIVER INITIALIZATION
*
INIT	CLR CURDRV
	CLR TRKREG
	CLR SECREG
	CLR VOLREG
	CLR NUMBUF
WARM	RTS		WARM START NOT NEEDED
*
* RESTORE RESTORES THE HEAD TO 00
*
RESTOR	BSR DRIVE
	BNE .1		ERROR EXIT
	LDA #0
	LDB #1
	BRA SEEK
.1	RTS
*
* READ ONE SECTOR
*
READ	BSR SEEK
	LDA #RDCMND	RWTS READ COMMAND CODE
	STA COMREG
	BSR A2DOS
	BITB #RDMSK
	RTS
*
SEEK	INCA		SKIP A TRACK FOR AP2FLEX
	STA TRKREG
	CMPA #34	MAX TRACK
	BGT ERROR
	DECB
	STB SECREG
	CMPB #15	MAX SECTOR
	BGT ERROR
	PSHS X
	LDX #SECMAP	POINT TO CORRECT MAP
	LDB B,X
	PULS X
	STB SECREG
	CLR VOLREG
	CLR NUMBUF
	EXG D,X
	STB SECBUF
	EORA #$80
	STA SECBUF+1
	CLRB
	RTS
ERROR	LDB #$10	SEEK ERROR
	RTS
*
* WRITE ONE SECTOR
*
WRITE	BSR SEEK
	LDA #WTCMND
	STA COMREG
	BSR A2DOS
	BITB #WTMSK
	RTS
*
A2DOS	STA P.DOS	FOR 6502
IDLE	EXG A,A		IDLE
	TST P.DOS
	BNE IDLE	NOT READY YET
	CLRB
	LDA RETCODE
	BEQ L0		NO ERROR
	BITA #$10	CHECK WRITE PROTECT
	BNE L1
	ORB #$40	WRITE PROTECT ERROR
L1	BITA #$E0	CHECK DISK ERROR
	BEQ L0		NO OTHER ERROR
	ORB #$1C
L0	RTS
*
VERIFY	CLRB
	RTS

DRIVE	LDA 3,X		GET DRIVE NUMBER
	CMPA #1		ENSURE IT'S 0 or 1
	BLS DRV2	BRANCH IF OK
	LDB #$0F	ELSE SET ERROR VALUE
	ORCC #1
	RTS
DRV2	INCA
	STA CURDRV
	BRA OK

QUICK
CHKRDY	LDA 3,X		GET DRIVE NUMBER
	CMPA #1		ENSURE IT'S 0 or 1
	BLS OK		BRANCH IF OK
	LDB #$80	ELSE, SHOW NOT READY
	ORCC #1
	RTS		RETURN
OK	CLRB		SHOW NO ERROR
	ANDCC #$FE
	RTS
*
SECMAP	.HS	00060C03090F0E050B0208070D040A01
*
	.EN
