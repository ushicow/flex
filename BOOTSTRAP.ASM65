*
* BOOTSTRAP
*
* IPL FOR AP10 6809 CARD
* 6502 CODE
*
* Nov.27,2021 NANJA.INFO
*
	.OR $7700
	.TF BOOTSTRAP
	.IN APPLE.EQU

* RWTS AND IOB
*
RWTS	.EQ $77B5	DOS3.3 RWTS
IOB	.EQ $77E8	DOS3.3 IOB
DSLOT	.EQ IOB+1	DISK II SLOT NO. TIMES 16
CURDRV	.EQ IOB+2	DRIVE NUM
VOLREG	.EQ IOB+3	VOLUME NUM (0 IS ANY)
TRKREG	.EQ IOB+4	TRACK NUM
SECREG	.EQ IOB+5	SECTOR NUM
SECBUF	.EQ IOB+8	DATA BUFFER
COMREG	.EQ IOB+$C	READ=1, WRITE=2, INIT=3
RETCODE	.EQ IOB+$D	ERROR CODE
LSTSLOT	.EQ IOB+$F	PREVIOUS SLOT NUM ACCESSED
LSTDRV	.EQ IOB+$10	PREVIOUS DRIVE NUM ACCESSED
*
BOOT2	STX DSLOT	SET DISK I/F SLOT
	STX LSTSLOT	SET DISK I/F SLOT
	JSR SEARCH
	LDA #1		
	STA CURDRV	 DRIVE 1
	STA LSTDRV	 DRIVE 1
	STA COMREG	 READ COMMAND
	LDA #0
	STA VOLREG	 ANY VOLUME
	STA TRKREG	 TRACK 0
	STA SECBUF	 SECTOR BUFFER ADDRESS IS $xx00

	LDA #$A		SECTOR A: BIOS6502
	STA SECREG
	LDA #$60	  READ TO $6000
	JSR RDSEC
	
	LDA #$B		SECTOR B: DISK DRIVER
	STA SECREG
	LDA #$5E   	  READ TO $5E00 (6809 $DE00)
	JSR RDSEC

	LDA #$C		SECTOR C: CONSOLE DRIVER
	STA SECREG
	LDA #$53   	  READ TO $5300 (6809 $D300)
	JSR RDSEC

	LDA #$D		SECTOR D: AP10BIOS
	STA SECREG
	LDA #$61	  READ TO $6100
	JSR RDSEC

	LDA #$E		SECTOR E: MILLBIOS
	STA SECREG
	LDA #$62	  READ TO $6200
	JSR RDSEC

	LDA #$F		SECTOR D: LOADER
	STA SECREG
	LDA #63		  READ TO $6300
	JSR RDSEC

	LDA #$1		TRACK 1 / SECTOR 0 FLEXLOAD
	STA TRKREG
	LDA #$0
	STA SECREG
	LDA #$41	  READ TO $4100 (6809 $C100)
	JSR RDSEC

	LDA CARDID
	CMP #IDMILL
	BEQ MILL
	JMP APBIOS
MILL	JMP MLBIOS
*
* READ A SECTOR FROM DISK TO SECBUF MEMORY
* 
RDSEC	STA SECBUF+1	READ A SECTOR, AREG IS HIGH MEM ADDRESS
	LDA #IOB/256	
	LDY #IOB
	JMP RWTS
ZZA	.EQ *
ZZAFREE	.EQ RWTS-ZZA	CHECK MEMORY OVER RUN

	.OR $85D	USE APPEND BUG PATCHES AREA
	.TF SLOTSEARCH
*
* SERACH AP10 CARD
*
SEARCH	JSR HOME
	LDX #$10	SLOT #1
NSLOT	LDA #$02
	STA RAM04,X	SET RAM
	LDA #$AA
	STA $E000
	LDA $E000
	CMP #$AA
	BEQ FOUND
	TXA		X=X+$10
	CLC
	ADC #$10
	TAX
	CMP #$80
	BNE NSLOT	NEXT SLOT
	LDA #NOCARD
	STA ADDR
	LDA #NOCARD/256
	STA ADDR+1
	JSR PRINT
	RTS		DONE
FOUND	LDA #IDAP10
	STA CARDID
	STA HALT,X	SET ROM
	TXA
	STA CARD	STORE AP10 SLOT NUMBER X 16
	LSR		A/2
	LSR		A/4
	LSR		A/8
	LSR		A/16
	CLC
	ADC #'0+$80
	STA SLOTN
	LDA #AP10MES
	STA ADDR
	LDA #AP10MES/256
	STA ADDR+1
	JSR PRINT
	LDA #MES
	STA ADDR
	LDA #MES/256
	STA ADDR+1
	JSR PRINT
	RTS
*
CR	.EQ $8D
BELL	.EQ $87
AP10MES	.AS "AP10"
	.DA #0
MILLMES .AS "THE MILL"
        .DA #0
MES	.AS " CARD IN SLOT #"
SLOTN	.DA #'0
	.DA #CR
	.DA #0
NOCARD	.DA #BELL
	.AS "NO 6809 CARD FOUND"
	.DA #0
*
* PRINT MESSAGE
*   ADDR = MESSAGE TEXT ADDRESS
*
PRINT	LDY #$FF
P1	INY
	LDA (ADDR),Y
	ORA #$80
	JSR COUT
	ASL
	BNE P1
	RTS
*
ZZB	.EQ *
ZZBFREE	.EQ $8FE-ZZB	CHECK MEMORY OVER RUN

	.EN
