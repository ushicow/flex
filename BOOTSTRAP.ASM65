*
* BOOTSTRAP
*
* IPL FOR AP10 6809 CARD
* 6502 CODE
*
* Nov.27,2021 NANJA.INFO
*
	.OR $7700
	.TF BOOTSTRAP
	.IN APPLE.EQU
*
* RWTS AND IOB
*
RWTS	.EQ $77B5	DOS3.3 RWTS
IOB	.EQ $77E8	DOS3.3 IOB
DSLOT	.EQ IOB+1	DISK II SLOT NO. TIMES 16
CURDRV	.EQ IOB+2	DRIVE NUM
VOLREG	.EQ IOB+3	VOLUME NUM (0 IS ANY)
TRKREG	.EQ IOB+4	TRACK NUM
SECREG	.EQ IOB+5	SECTOR NUM
SECBUF	.EQ IOB+8	DATA BUFFER
COMREG	.EQ IOB+$C	READ=1, WRITE=2, INIT=3
RETCODE	.EQ IOB+$D	ERROR CODE
LSTSLOT	.EQ IOB+$F	PREVIOUS SLOT NUM ACCESSED
LSTDRV	.EQ IOB+$10	PREVIOUS DRIVE NUM ACCESSED
*
BOOT2	STX DSLOT	SET DISK I/F SLOT
	STX LSTSLOT	SET DISK I/F SLOT
	JSR SEARCH
	LDA #1
	STA CURDRV	 DRIVE 1
	STA LSTDRV	 DRIVE 1
	STA COMREG	 READ COMMAND
	LDA #0
	STA VOLREG	 ANY VOLUME
	STA TRKREG	 TRACK 0
	STA SECBUF	 SECTOR BUFFER ADDRESS IS $xx00
*
	LDA #$A		SECTOR A: FLEXBIOS
	STA SECREG
	LDA #$60	  READ TO $6000
	JSR RDSEC
*	
	INC SECREG	SECTOR B: DISK DRIVER
	LDA #$5E   	  READ TO $5E00 (6809 $DE00)
	JSR RDSEC
*
	INC SECREG	SECTOR C: CONSOLE DRIVER
	LDA #$53   	  READ TO $5300 (6809 $D300)
	JSR RDSEC
*
	INC SECREG	SECTOR D: AP10BIOS
	LDA #$61	  READ TO $6100
	JSR RDSEC
*
	INC SECREG	SECTOR E: MILLBIOS
	LDA #$62	  READ TO $6200
	JSR RDSEC
*
	INC SECREG	SECTOR F: FLEXLOAD
	LDA #$63	  READ TO $6300
	JSR RDSEC
*
	LDA #$1		TRACK 1 / SECTOR 0 FLEXLOAD
	STA TRKREG
	LDA #$0
	STA SECREG
	LDA #$41	  READ TO $4100 (6809 $C100)
	JSR RDSEC
*
	LDA CARDID
	BEQ MILL
	JMP APBIOS
MILL	JMP MLBIOS
*
CR	.EQ $8D
AP10MES	.AS "AP10"
	.DA #0
MILLMES .AS "THE MILL"
        .DA #0
MES	.AS " CARD IN SLOT #"
SLOTN	.DA #'0
	.DA #CR
	.AS "FLEXBIOS V2.0"
	.DA #CR
	.DA #0
BELL	.EQ $87
NOCARD	.DA #BELL
	.AS "NO 6809 CARD FOUND"
	.DA #0
*
ZZZA	.EQ RWTS-*
*
	.OR $765D	USE APPEND BUG PATCHES AREA
	.TF SLOTSEARCH
*
* SERACH AP10 CARD
*
SEARCH	JSR HOME
	LDA #$2C
	CMP $C300
	BNE NOT80
	LDA #0
	JSR $C300
NOT80	LDX #$10	SLOT #1
NSLOT	LDA #$02
	STA RAM04,X	SET RAM
	LDA #$AA
	STA $E000
	LDA $E000
	CMP #$AA
	BEQ FOUNDAP
	LDA $C080,X	CHECK MILL CARD
	CMP #$C0
	BEQ FOUNDML
	LDA $C058	ANNUNCIATOR #0 OFF
	TXA		X=X+$10
	CLC
	ADC #$10
	TAX
	CMP #$80
	BNE NSLOT	NEXT SLOT
	LDA #NOCARD
	STA ADDR
	LDA #NOCARD/256
	STA ADDR+1
	JSR PRINT	PRINT ERROR AND DONE
	BRK
*
FOUNDML LDA #IDMILL	SET CARDID = IDMILL
	STA CARDID
	LDA #MILLMES
	STA ADDR
	LDA #MILLMES/256
	STA ADDR+1
	JSR PRINT
	JMP FOUND
FOUNDAP	LDA #IDAP10	SET CARDID = IDAP10
	STA CARDID
	STA HALT,X	SET ROM
	LDA #AP10MES
	STA ADDR
	LDA #AP10MES/256
	STA ADDR+1
	JSR PRINT
FOUND	TXA
	STA CARD	STORE AP10 SLOT NUMBER X 16
	LSR		A/2
	LSR		A/4
	LSR		A/8
	LSR		A/16
	CLC
	ADC #'0+$80
	STA SLOTN
	LDA #MES
	STA ADDR
	LDA #MES/256
	STA ADDR+1
*	JMP PRINT	PRINT MES AND RETURN
*
* PRINT MESSAGE
*   ADDR = MESSAGE TEXT ADDRESS
*
PRINT	LDY #$FF
P1	INY
	LDA (ADDR),Y
	ORA #$80
	JSR COUT
	ASL
	BNE P1
	RTS
*
* READ A SECTOR FROM DISK TO SECBUF MEMORY
* 
RDSEC	STA SECBUF+1	READ A SECTOR, AREG IS HIGH MEM ADDRESS
	LDA #IOB/256	
	LDY #IOB
	JMP RWTS
*
ZZZB	.EQ $76FE-*
	.EN
