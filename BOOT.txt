*
* BOOT CODE TRACK 0 SECTOR 0
*
POINTA	.EQ $26		BUFFER POINTER
BSLOT	.EQ $28		BOOT BSLOT 
BSECTR	.EQ $3D		LAST BSECTOR READ
BTEMP	.EQ $3E		ADDRESS BTEMP
BRETRY	.EQ $5C		OFFSET TO READER		

MONINIT	.EQ $FB2F	MONINIT SCREEN

	.OR $800
	.DA #1
	LDA POINTA+1
	CMP #$09
	BNE READNEXT
* WE'VE BEEN BOOTED. SET UP
* PARAMS FOR BOOT PROM SO
* THAT WE'LL READ IN TRACK 0, 
* BSECTRS 00-09.
	LDA BSLOT
	LSR
	LSR
	LSR
	LSR
	ORA #$C0
	STA BTEMP+1
	LDA #BRETRY
	STA BTEMP
	CLC
	LDA LOADADDR+1
	ADC BGRPGC
	STA LOADADDR+1
* READ IN ANOTHER BSECTR FROM
* TRACK ZERO...
READNEXT	LDX BGRPGC
	BMI GOLOADER
	LDA TABLE,X
	STA BSECTR
	DEC BGRPGC
	LDA LOADADDR+1
	STA POINTA+1
	DEC LOADADDR+1
	LDX BSLOT
	JMP (BTEMP)
GOLOADER	INC LOADADDR+1
	INC LOADADDR+1
	JSR SETKBD
	JSR SETVID
	JSR MONINIT
	LDX BSLOT
	JMP (LOADADDR)
* TABLE OF PHYSICAL BSECTR NUMBERS
*  WHICH CORRESPOND TO THE LOGICAL
*  BSECTRS 0-F ON TRACK ZERO...
TABLE	.DA #0
	.DA #13
	.DA #11
	.DA #9
	.DA #7
	.DA #5
	.DA #3
	.DA #1
	.DA #14
	.DA #12
	.DA #10
	.DA #8
	.DA #6
	.DA #4
	.DA #2
	.DA #15
	.END

	.OR $8FD
	.DA $7600
	.DA #9
