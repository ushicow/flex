* APPLE2 BIOS FOR FLEX
*   KEYBOARD / DISPLAY CONTROL
*   APPLE DOS RWTS ROUTINES
*   FOR 6502 PROCESSOR
*
* NOV.28,2021 NANJA.INFO
*
*
* RWTS AND IOB
*
RWTS	.EQ $B7B5	DOS3.3 RWTS 48KB RAM
IOB	.EQ $B7E8	DOS3.3 IOB 48KB RAM
DSLOT	.EQ IOB+1	DISK II SLOT NO. TIMES 16
CURDRV	.EQ IOB+2	DRIVE NUM
VOLREG	.EQ IOB+3	VOLUME NUM (0 IS ANY)
TRKREG	.EQ IOB+4	TRACK NUM
SECREG	.EQ IOB+5	SECTOR NUM
SECBUF	.EQ IOB+8	DATA BUFFER
COMREG	.EQ IOB+$C	READ=1, WRITE=2, INIT=3
RETCODE	.EQ IOB+$D	ERROR CODE
LSTSLOT	.EQ IOB+$F	PREVIOUS SLOT NUM ACCESSED
LSTDRV	.EQ IOB+$10	PREVIOUS DRIVE NUM ACCESSED
*
	.IN APPLE.EQU
	.OR BIOS
	.TF BIOS6502
*
START	LDX CARD	SET XREG TO AP10 SLOT NUMBER X 16
	LDA #0		PREPARE TO ZERO OUT AND INITIALIZE
	STA RAMDF,X	HALT THE 6809
	STA KEYLOC	SET KEYLOC MAILBOX AS EMPTY
	STA TVLOC	SET TVLOC MAILBOX AS EMPTY
	STA FLAG	SET SEMAPHORE FLAG AS NOP
	STA PREG
	STA RUN,X	START THE 6809
*
SCAN	LDA KYBD	SET AREG TO KEYBOARD INPUT
	BPL DISP 	IF NO KEYIN GO CHECK TV OUT
	CMP #$82
	BEQ BREAK	IF CTL-B BREAK TO MONITOR
	LDA KEYLOC
	BNE DISP
	LDY STROBE
	AND #$7F
	STA KEYLOC
DISP	LDA TVLOC
	BEQ CONT	CONTINUE
	CMP #$0A	IGNORE LINE FEED
	BEQ SKIP
	ORA #$80
	STA ROM,X
	JSR COUT
	STA RUN,X
SKIP	LDA #0
	STA TVLOC
CONT	LDA FLAG	CHECK SEMAPHORE FLAG
	BEQ SCAN
	CMP #A.RWTS
	BEQ APDOS
	CMP #A.6502
	BEQ APPLE
BREAK	STA HALT,X
	BRK		BREAK TO APPLE II MONITOR
	NOP		NO OPARATION FOR BRK INSTRUCTION
	JMP START
*
APDOS	JMP APDOS.A
APPLE	JMP APPLE.A
	.BS APBIOS-*	FILL UP
*
*
* BIOS FOR AP10
*
*	.OR APBIOS
*	.TF AP10BIOS
	LDX CARD
	STA RAM8C,X
	LDA #0
	STA ADDR
	STA DEST
	LDA #$41
	STA ADDR+1
	LDA #$F1
	STA DEST+1
	JSR COPYMEM

	STA RAMDF,X	COPY APPLE2 RAM $5000-7FFF TO AP10 RAM $D000-FFFF
	LDA #$50
	STA ADDR+1
	LDA #$D0
	STA DEST+1
NEXT	JSR COPYMEM
	INC ADDR+1
	INC DEST+1
	BNE NEXT
	LDA #$C1	SET 6809 RESET VECTOR TO $C100
	STA $FFFE
	LDA #0
	STA $FFFF
	STA HALT,X

	LDA #SECTBUF
	STA SECBUF
	LDA #SECTBUF/256
	STA SECBUF+1
*
	JMP START

APDOS.A	LDA RAMDF,X
	LDA DRVNUM
	STA CURDRV
	LDA #0
	STA VOLREG
	LDA TRKNUM
	STA TRKREG
	LDA SECNUM
	STA SECREG
	LDA COMMAND
	STA COMREG
	CMP #1		READ
	BEQ GORWTS
	LDY #0		COPY WRITE DATA
SETBUF	LDA FILEBUF,Y
	STA SECTBUF,Y
	INY
	BNE SETBUF
GORWTS	LDA #IOB/256	CALL RWTS
	LDY #IOB
	STA HALT,X
	JSR RWTS
	BCS ONERR
	LDA #0		IF NO ERROR
	STA RETCODE	CLEAR IOB RETURN CODE
ONERR	LDX CARD
	STA RAMDF,X
	LDA RETCODE	SET ERROR CODE
	STA ERRCODE
	LDA COMREG
	CMP #1		READ
	BNE DONE
	LDY #0		COPY READ DATA
GETBUF	LDA SECTBUF,Y
	STA FILEBUF,Y
	INY
	BNE GETBUF
DONE	JMP START
*
* COPY A PAGE MEMORY FROM ADDR TO DEST
*
COPYMEM	LDY #0
:1	LDA (ADDR),Y
	STA (DEST),Y
	INY
	BNE :1
	RTS
*
APPLE.A	LDA A2SUB	SET JSR XXXX
	STA GO+2
	LDA A2SUB+1
	STA GO+1
	LDA PREG
	PHA
	LDA XREG
	PHA
	LDA AREG
	LDA YREG
	STA HALT,X
	PLA
	TAX
	PLP
	CLI		ENABLE 6502 INTERRUPTS
GO	JSR XXXX	RUN THE 6502 SUBROUTINE
	PHP
	SEI		DISABLE 6502 INTERRUPTS
	PHA
	TXA
	PHA
	LDX CARD
	STA RAMDF,X
	PLA
	STA XREG
	PLA
	STA XREG
	STY YREG
	PLA
	STA PREG
	JMP START
XXXX	RTS
ZZZZ	.EN
