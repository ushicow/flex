* ASSEMBLY LANGUAGE ROUTINES
* KEYBOARD AND DISPLAY CONTROL
* 6502 PROCESSOR
*
	.OR $1000
	.TF MILLBIOS
*
SLOT	.EQ 5
BA	.EQ SLOT*16+$C087	BA SIGNAL FROM 6809 WILL BE LOW WHEN RUNNING
BS	.EQ SLOT*16+$C086	BS SIGNAL (SEE MC6809 DATA SHEET)
IRQ	.EQ SLOT*16+$C085	IRQ STATUS INTO 6809 (INTERRUPT)
FIRQ	.EQ SLOT*16+$C084	FIRQ REQUEST TO 6809 (INTERRUPT)
NMI	.EQ SLOT*16+$C083	NMI REQUEST TO 6809 (INTERRUPT)
RESET	.EQ SLOT*16+$C082	RESET REQUEST TO 6809 (LOW TO RESET)
HALT	.EQ SLOT*16+$C081	HALT REQUEST TO 6809 (LOW TO STOP)
IRQ6502	.EQ SLOT*16+$C080	IRQ TO 6502 (HIGH TO INTERRUPT)
RWTS	.EQ $37B5
RDKEY	.EQ $FD0C
MEMEND	.EQ $CC2B-$8000		FLEX MEMEND

KEYLOC	.EQ $06
TVLOC	.EQ $07
DOSLOC	.EQ $08
FLAG	.EQ $09
F.IN	.EQ 1
F.STOP	.EQ $FF
*
	LDA #$C1	SET 6809 RESET VECTOR FOR QLOAD
	STA $7FFE
	LDA #$00
	STA $7FFF
	LDA #$3B	6809 RTI command
	STA $7FF0
	LDA #$39	6809 RTS command
	STA $7FF1
	LDA #$FF
	STA $7FF2 	SW3
	STA $7FF4	SW2
	STA $7FF6	FIRQ
	STA $7FF8	IRQ
	STA $7FFA	SW1
	STA $7FFC	NMI
	LDA #$F0
	STA $7FF3
	STA $7FF5
	STA $7FF7
	STA $7FF9
	STA $7FFB
	STA $7FFD
	LDA #$3F	SET MEMEND to $3FFF
	STA MEMEND
*
	LDA #$00	;PREPARE TO ZERO OUT AND INITIALIZE
	STA HALT	;HALT THE 6809
	STA RESET	;RESET THE 6809
	STA BS		;DISABLE THE MILLS ROM-ENABLE MAPPING
	STA KEYLOC	;SET KEYLOC MAILBOX AS EMPTY
	STA TVLOC	;SET TVLOC MAILBOX AS EMPTY
	STA DOSLOC
	STA FLAG
	LDA #$80	;PREPARE TO RAISE THE CONTROL LINES
	STA NMI		;RAISE 6809 NMI (NO INTERRUPT)
	STA IRQ		;RAISE 6809 IRQ (NO INTERRUPT)
	STA FIRQ	;RAISE 6809 FIRQ (NO INTERRUPT)
	STA HALT	;START THE 6809
	STA RESET	;AND ALLOW TO PROCEED OUT OF RESET MODE
SCAN	LDA FLAG
	CMP #F.STOP
	BEQ BREAK
	CMP #F.IN
	BEQ KEYIN
	LDA $C000	;PROCEDURE SCANKEY;
	SEI
	NOP
	BPL EXIT	;IF BYTE_AT ($C000) LESS THAN 128 THEN EXIT
	CMP #$82	;IF KEY PRESSED IS CONTROL-B THEN
	BEQ BREAK		;GOTO BREAK TO APPLE MONITOR
	LDX KEYLOC	;IF KEYLOC <> 0 THEN
	BNE EXIT		;EXIT
	LDX $C010	;CLEAR KEYBOARD STOROBE
	AND #$7F	;CONVERT CHARACTER IN A TO TRUE ASCII
	STA KEYLOC	;STORE IN KEYLOC
	JMP EXIT
KEYIN	JSR RDKEY
	AND #$7F
	STA KEYLOC
	LDA #0
	STA FLAG
	JMP SCAN
BREAK	LDX $C010	;BREAK TO APPLE MONITOR ON OPERATOR 
*			;PRESS OF CONTROL-B
	LDA #$00
	STA HALT	;HALT 6809 SO WE CAN POKE SAFELY AROUND
*			;CHARRED REMAINS OF POSSIBLE PROGRAM CRASH
	STA IRQ6502	;CLEAR POSSIBLE 6502 INTERRUPT REQUEST
	BRK		;BREAK TO APPLE II MONITOR
	NOP		;NO OPARATION FOR BRK INSTRUCTION
	LDA #0
	STA FLAG
	LDA #$80	;WE GET HERE ONLY IF OPERATOR HAS DONE A
*			; G (GO) FROM APPLE MONITOR
	STA HALT	;START UP 6809 AGAIN
	NOP
EXIT	LDA TVLOC	;PROCEDURE DISPLAY
	BEQ APDOS	;IF TVLOC IS EMPTY THEN DOS
	CMP #$0A	;WE MAY IGNORE IT ON TV DISPLAY
	BEQ SKIP
	CMP #$60	;IS CHARACTER IN LOWERCASE-IF SO CONVERT TO UPPER
	BCC PRINT	;IF NOT THEN JUST PRINT IT
	AND #$5F	;CONVERT LOWER TO UPPER CASE
PRINT	ORA #$80	;SET HIGH ORDER BIT FOR NOMAL TEXT ON APPLE
	JSR $FDF0	;CALL MONITOR COUT1-(TV DISPLAY SUBROUTINE)
SKIP	LDA #$00
	STA TVLOC	;CLEAR TVLOC MAILBOX
	JMP SCAN
*
APDOS	LDA DOSLOC
	BEQ SCAN
	LDA #0
	STA HALT
	LDA #$37
	LDY #$E8
	JSR RWTS
	BCS .1		;IF ERROR
	LDA #0
	STA $37E8+$D	;IOB RETURN CODE
.1	LDA #$80
	STA HALT
	LDA #0
	STA $48
	STA DOSLOC
	JMP SCAN

	.EN
