* APPLE2 BIOS FOR FLEX
*   KEYBOARD / DISPLAY CONTROL
*   APPLE DOS RWTS ROUTINES
*   FOR 6502 PROCESSOR
*
MFLIP	.EQ 0		6502 ADDRESS
	.IN APPLE.EQU
	.OR BIOS
	.TF MILLBIOS
*
	LDX MILL	SET XREG TO THE MILL SLOT NUMBER	
	LDA #$00	PREPARE TO ZERO OUT AND INITIALIZE
	STA HALT,X	 HALT THE 6809
	STA RESET,X	 RESET THE 6809
	STA ROM,X	 DISABLE THE MILLS ROM-ENABLE MAPPING
	STA KEYLOC	 SET KEYLOC MAILBOX AS EMPTY
	STA TVLOC	 SET TVLOC MAILBOX AS EMPTY
	STA FLAG	 SEMAPHORE FLAG
	LDA #$80	PREPARE TO RAISE THE CONTROL LINES
	STA NMI,X	 RAISE 6809 NMI (NO INTERRUPT)
	STA IRQ,X	 RAISE 6809 IRQ (NO INTERRUPT)
	STA FIRQ,X	 RAISE 6809 FIRQ (NO INTERRUPT)
	STA HALT,X	 START THE 6809
	STA RESET,X	 AND ALLOW TO PROCEED OUT OF RESET MODE
	JMP SCAN

RESTART	LDX MILL
	LDA #0
	STA FLAG
	LDA #$80
	STA HALT,X	START UP 6809
*
SCAN	LDA KYBD	CHECK KEYBOARD
	BPL DISP	 IF NOT CHECK TV OUT
	CMP #$82
	BEQ BREAK
	LDX KEYLOC
	BNE DISP
	LDX STROBE
	AND #$7F
	STA KEYLOC
DISP	LDA TVLOC
	BEQ CONT	CONTINUE
	CMP #$0A	IGNORE LINE FEED
	BEQ SKIP
	ORA #$80
	JSR COUT1
SKIP	LDA #$00
	STA TVLOC
CONT	LDA FLAG	CHECK SEMAPHORE FLAG
	BEQ SCAN
BREAK	LDX MILL
	LDA #$0
	STA HALT,X
	LDA FLAG
	CMP #F.RWTS
	BEQ APDOS
	CMP #F.6502
	BEQ APPLE
	BRK		BREAK TO APPLE II MONITOR
	NOP		NO OPARATION FOR BRK INSTRUCTION
	JMP RESTART
*
APDOS	LDA #IOB/256	CALL RWTS
	LDY #IOB
	JSR RWTS
	BCS NOERR
	LDA #0		IF NO ERROR
	STA IOB+$D	 CLEAR IOB RETURN CODE
NOERR	JMP RESTART
*
APPLE	LDA A2SUB	SET JSR XXXX
	STA GO+2
	LDA A2SUB+1
	STA GO+1
	JSR RESTORE	RESTORE 6502 REGISTOR	CLI		ENABLE 6502 INTERRUPTSGO	JSR XXXX	RUN THE 6502 SUBROUTINE	SEI		DISABLE 6502 INTERRUPTS	JSR SAVE	STORE 6502 REGISTORS
	JMP RESTART
XXXX	RTSZZZ	.EQ *
	.EN
