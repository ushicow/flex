* APPLE2 BIOS FOR FLEX
*   KEYBOARD / DISPLAY CONTROL
*   APPLE DOS RWTS ROUTINES
*   FOR 6502 PROCESSOR
*
* NOV.28,2021 NANJA.INFO
*
*
* RWTS AND IOB
*
RWTS	.EQ $77B5	DOS3.3 RWTS 32KB RAM
IOB	.EQ $77E8	DOS3.3 IOB 32KB RAM
DSLOT	.EQ IOB+1	DISK II SLOT NO. TIMES 16
CURDRV	.EQ IOB+2	DRIVE NUM
VOLREG	.EQ IOB+3	VOLUME NUM (0 IS ANY)
TRKREG	.EQ IOB+4	TRACK NUM
SECREG	.EQ IOB+5	SECTOR NUM
SECBUF	.EQ IOB+8	DATA BUFFER
COMREG	.EQ IOB+$C	READ=1, WRITE=2, INIT=3
RETCODE	.EQ IOB+$D	ERROR CODE
LSTSLOT	.EQ IOB+$F	PREVIOUS SLOT NUM ACCESSED
LSTDRV	.EQ IOB+$10	PREVIOUS DRIVE NUM ACCESSED
*
	.IN APPLE.EQU
	.OR BIOS
	.TF FLEXBIOS
*
*
	LDA CARDID
	BEQ STARTAP
	JMP RESTART 
*
STARTAP	LDX CARD
	LDA #0
	STA RAMDF,X	HALT THE 6809
	STA FLAG	SET SEMAPHORE FLAG AS NOP
	STA PREG
	STA RUN,X	START THE 6809
*
SCANAP	LDA KYBD	CHEKC KEYBOARD
	BMI DISPAP
	LDA #0
DISPAP	STA KEYLOC
	LDA TVLOC
	BEQ CONTAP	CONTINUE
	CMP #$0A	IGNORE LINE FEED
	BEQ SKIPAP
	CMP #$11	IGNORE CTL-Q
	BEQ SKIPAP
	CMP #$15	IGNORE CTL-U
	BEQ SKIPAP
	ORA #$80
	STA HALT,X
	JSR COUT
	STA RUN,X
SKIPAP	LDA #0
	STA TVLOC
CONTAP	LDA FLAG	CHECK SEMAPHORE FLAG
	BEQ SCANAP
	CMP #F.KEYIN
	BEQ KEYINAP
	CMP #F.RWTS
	BEQ APDOS
	CMP #F.6502
	BEQ AP6502
BREAKAP	STA HALT,X
	BRK		BREAK TO APPLE II MONITOR
	NOP		NO OPARATION FOR BRK INSTRUCTION
	JMP STARTAP
KEYINAP	LDA KYBD	SET AREG TO KEYBOARD INPUT
	BPL KEYINAP
	LDY STROBE
	CMP #$82
	BEQ BREAKAP	IF CTL-B BREAK TO MONITOR
	AND #$7F
	STA AREG
	JMP STARTAP
*
APDOS	JMP APDOS.A
AP6502	JMP APPLE.A
*
SCANML	LDA KYBD	CHECK KEYBOARD
	BMI DISPML
	LDA #0
DISPML	STA KEYLOC-$8000
	LDA TVLOC-$8000
	BEQ CONTML	CONTINUE
	CMP #$0A	IGNORE LINE FEED
	BEQ SKIPML
	CMP #$11	IGNORE CTL-Q
	BEQ SKIPML
	CMP #$15	IGNORE CTL-U
	BEQ SKIPML
	LDA #0
	LDA MLHALT,X
	LDA $C082
	LDA TVLOC-$8000
	ORA #$80
	JSR COUT
	LDA $C083
	LDA $C083
	LDA #$80
	LDA MLHALT,X
SKIPML	LDA #0
	STA TVLOC-$8000
CONTML	LDA FLAG-$8000	CHECK SEMAPHORE FLAG
	BEQ SCANML
	CMP #F.KEYIN
	BEQ KEYINML
	CMP #F.RWTS
	BEQ MLDOS
	CMP #F.6502
	BEQ ML6502
BREAKML	LDA #0
	STA MLHALT,X
	LDA $C082
	BRK		BREAK TO APPLE II MONITOR
	NOP		NO OPARATION FOR BRK INSTRUCTION
	JMP RESTART
KEYINML	LDA KYBD	SET AREG TO KEYBOARD INTERRUPTS
	BPL KEYINML
	LDY STROBE
	CMP #$82
	BEQ BREAKML	IF CTL-B BREAK TO MONITOR
	AND #$7F
	STA AREG-$8000
	JMP RESTART
*
MLDOS	JMP APDOS.M
ML6502	JMP APPLE.M
*
* COPY A PAGE MEMORY FROM ADDR TO DEST
*
COPYMEM	LDY #0
:1	LDA (ADDR),Y
	STA (DEST),Y
	INY
	BNE :1
	RTS
*
ZZZBIOS	.EQ BIOS+$100-*
*
*
* BIOS FOR AP10
*
	.OR APBIOS
	.TF AP10BIOS
	LDX CARD
	STA RAM8C,X	COPY APPLE2 RAM $4100-41FF TO AP10 RAM $C100-C1FF
	LDA #0
	STA ADDR
	STA DEST
	LDA #$41
	STA ADDR+1
	LDA #$F1
	STA DEST+1
	JSR COPYMEM
*
	STA RAMDF,X	COPY APPLE2 RAM $5300-7FFF TO AP10 RAM $D300-FFFF
	LDA #$53
	STA ADDR+1
	LDA #$D3
	STA DEST+1
NEXT	JSR COPYMEM
	INC ADDR+1
	INC DEST+1
	BNE NEXT
	LDA #$C1	SET 6809 RESET VECTOR TO $C100
	STA RSTVCT
	LDA #0
	STA RSTVCT+1
	STA FLIP
	STA RESERVE
	STA HALT,X
*
	LDA #SECTBUF
	STA SECBUF
	LDA #SECTBUF/256
	STA SECBUF+1
*
	LDA #0		PREPARE TO ZERO OUT AND INITIALIZE
	STA RAMDF,X	HALT THE 6809
	STA KEYLOC	SET KEYLOC MAILBOX AS EMPTY
	STA TVLOC	SET TVLOC MAILBOX AS EMPTY
	JMP STARTAP
*
APDOS.A	LDA RAMDF,X
	LDA DRVNUM
	STA CURDRV
	LDA #0
	STA VOLREG
	LDA TRKNUM
	STA TRKREG
	LDA SECNUM
	STA SECREG
	LDA COMMAND
	STA COMREG
	CMP #2		WRITE?
	BNE GORWTS
	LDY #0		COPY WRITE DATA
SETBUF	LDA FILEBUF,Y
	STA SECTBUF,Y
	INY
	BNE SETBUF
GORWTS	LDA #IOB/256	CALL RWTS
	LDY #IOB
	STA HALT,X
	JSR RWTS
	BCS ONERR
	LDA #0		IF NO ERROR
	STA RETCODE	CLEAR IOB RETURN CODE
ONERR	LDX CARD
	STA RAMDF,X
	LDA RETCODE	SET ERROR CODE
	STA ERRCODE
	LDA COMREG
	CMP #1		READ?
	BNE DONE
	LDY #0		COPY READ DATA
GETBUF	LDA SECTBUF,Y
	STA FILEBUF,Y
	INY
	BNE GETBUF
DONE	JMP STARTAP
*
APPLE.A	LDA A2SUB	SET JSR XXXX
	STA GOAP+2
	LDA A2SUB+1
	STA GOAP+1
	LDA PREG
	PHA
	LDA XREG
	PHA
	LDA AREG
	LDY YREG
	STA HALT,X
	PLA
	TAX
	PLP
	CLI		ENABLE 6502 INTERRUPTS
GOAP	JSR XXXX	RUN THE 6502 SUBROUTINE
	PHP
	SEI		DISABLE 6502 INTERRUPTS
	PHA
	TXA
	PHA
	LDX CARD
	STA RAMDF,X
	PLA
	STA XREG
	PLA
	STA AREG
	STY YREG
	PLA
	STA PREG
	JMP STARTAP
XXXX	RTS
ZZZAP10	.EQ APBIOS+$100-*
*
* BIOS ROUTINE FOR THE MILL
*
	.OR MLBIOS
	.TF MILLBIOS
*
	LDA #$C1	SET 6809 RESET VECTOR FOR FLEXLOAD
	STA RSTVCT-$8000
	LDA #$80
	STA FLIP-$8000
	LDA #$00
	STA RSTVCT+1-$8000
	STA RESERVE-$8000
	JMP STARTML
*
APDOS.M	LDA DRVNUM-$8000
	STA CURDRV
	LDA TRKNUM-$8000
	STA TRKREG
	LDA SECNUM-$8000
	STA SECREG
	LDA COMMAND-$8000
	STA COMREG
	LDA #0
	STA VOLREG
	STA MLHALT,X
	LDA #IOB/256	CALL RWTS
	LDY #IOB
	JSR RWTS
	BCS NOERR
	LDA #0		IF NO ERROR
	STA RETCODE	CLEAR IOB RETURN CODE
NOERR	LDA RETCODE	SET ERROR CODE
	STA ERRCODE-$8000
	JMP RESTART
*
APPLE.M	LDA #0
	STA MLHALT,X
	LDA $C082
	LDA A2SUB-$8000	SET JSR XXXX
	STA GOML+2
	LDA A2SUB+1-$8000
	STA GOML+1
	LDA PREG-$8000
	PHA
	LDA AREG-$8000
	LDX XREG-$8000
	LDY YREG-$8000
	PLP
	CLI		ENABLE 6502 INTERRUPTS
GOML	JSR XXXX	RUN THE 6502 SUBROUTINE
	PHP
	SEI		DISABLE 6502 INTERRUPTS
	STA AREG-$8000
	STX XREG-$8000
	STY YREG-$8000
	PLA
	STA PREG-$8000
	JMP RESTART
*
STARTML	LDX CARD
	LDA #$00	PREPARE TO ZERO OUT AND INITIALIZE
	STA MLHALT,X	 HALT THE 6809
	STA MLRESET,X	 RESET THE 6809
	STA MLROM,X	 DISABLE THE MILLS ROM-ENABLE MAPPING
	STA KEYLOC-$8000 SET KEYLOC MAILBOX AS EMPTY
	STA TVLOC-$8000	 SET TVLOC MAILBOX AS EMPTY
	STA FLAG-$8000	 SEMAPHORE FLAG
	STA PREG-$8000
	LDA $C083
	LDA $C083
	LDA #$80	PREPARE TO RAISE THE CONTROL LINES
	STA MLNMI,X	 RAISE 6809 NMI (NO INTERRUPT)
	STA MLIRQ,X	 RAISE 6809 IRQ (NO INTERRUPT)
	STA MLFIRQ,X	 RAISE 6809 FIRQ (NO INTERRUPT)
	STA MLHALT,X	 START THE 6809
	STA MLRESET,X	 AND ALLOW TO PROCEED OUT OF RESET MODE
	JMP SCANML
RESTART	LDA $C083
	LDA $C083
	LDX CARD
	LDA #0
	STA FLAG-$8000
	LDA #$80
	STA MLHALT,X	START UP 6809
	JMP SCANML
ZZZMILL	.EQ MLBIOS+$100-*
	.EN
