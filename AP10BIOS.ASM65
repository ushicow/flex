* APPLE2 BIOS FOR AP10 FLEX
*   KEYBOARD / DISPLAY CONTROL
*   APPLE DOS RWTS ROUTINES
*   FOR 6502 PROCESSOR
*
* NOV.20,2021 NANJA.INFO
*
*
* RWTS AND IOB
*
RWTS	.EQ $B7B5	DOS3.3 RWTS
IOB	.EQ $B7E8	DOS3.3 IOB
DSLOT	.EQ IOB+1	DISK II SLOT NO. TIMES 16
CURDRV	.EQ IOB+2	DRIVE NUM
VOLREG	.EQ IOB+3	VOLUME NUM (0 IS ANY)
TRKREG	.EQ IOB+4	TRACK NUM
SECREG	.EQ IOB+5	SECTOR NUM
SECBUF	.EQ IOB+8	DATA BUFFER
COMREG	.EQ IOB+$C	READ=1, WRITE=2, INIT=3
RETCODE	.EQ IOB+$D	ERROR CODE
LSTSLOT	.EQ IOB+$F	PREVIOUS SLOT NUM ACCESSED
LSTDRV	.EQ IOB+$10	PREVIOUS DRIVE NUM ACCESSED
*
	.IN APPLE.EQU
	.OR BIOS
	.TF AP10BIOS
*
	LDA #SECTBUF
	STA SECBUF
	LDA #SECTBUF/256
	STA SECBUF+1
*
START	LDX AP10	SET XREG TO AP10 SLOT NUMBER X 16
	LDA #0		PREPARE TO ZERO OUT AND INITIALIZE
	STA RAMDF,X	HALT THE 6809
	STA KEYLOC	SET KEYLOC MAILBOX AS EMPTY
	STA TVLOC	SET TVLOC MAILBOX AS EMPTY
	STA FLAG	SET SEMAPHORE FLAG AS NOP
	STA RUN,X	START THE 6809
*
SCAN	LDA KYBD	SET AREG TO KEYBOARD INPUT
	BPL DISP 	IF NO KEYIN GO CHECK TV OUT
	CMP #$82
	BEQ BREAK	IF CTL-B BREAK TO MONITOR
	LDA KEYLOC
	BNE DISP
	LDY STROBE
	AND #$7F
	STA KEYLOC
DISP	LDA TVLOC
	BEQ CONT	CONTINUE
	CMP #$0A	IGNORE LINE FEED
	BEQ SKIP
	ORA #$80
	STA ROM,X
	JSR COUT
	STA RUN,X
SKIP	LDA #0
	STA TVLOC
CONT	LDA FLAG	CHECK SEMAPHORE FLAG
	BEQ SCAN
	CMP #F.RWTS
	BEQ APDOS
	CMP #F.6502
	BEQ APPLE
BREAK	STA HALT,X
	BRK		BREAK TO APPLE II MONITOR
	NOP		NO OPARATION FOR BRK INSTRUCTION
	JMP START
*
APDOS	LDA RAMDF,X
	LDA DRVNUM
	STA CURDRV
	LDA #0
	STA VOLREG
	LDA TRKNUM
	STA TRKREG
	LDA SECNUM
	STA SECREG
	LDA COMMAND
	STA COMREG
	CMP #1		READ
	BEQ GORWTS
	LDY #0		COPY WRITE DATA
SETBUF	LDA FILEBUF,Y
	STA SECTBUF,Y
	INY
	BNE SETBUF
GORWTS	LDA #IOB/256	CALL RWTS
	LDY #IOB
	STA HALT,X
	JSR RWTS
	BCS ONERR
	LDA #0		IF NO ERROR
	STA RETCODE	CLEAR IOB RETURN CODE
ONERR	LDA #0
	STA PREG0
	LDX AP10
	STA RAMDF,X
	LDA RETCODE	SET ERROR CODE
	STA ERRCODE
	LDA COMREG
	CMP #1		READ
	BNE DONE
	LDY #0		COPY READ DATA
GETBUF	LDA SECTBUF,Y
	STA FILEBUF,Y
	INY
	BNE GETBUF
DONE	JMP START
*
APPLE	LDA A2SUB	SET JSR XXXX
	STA GO+2
	LDA A2SUB+1
	STA GO+1
	LDA AREG
	STA AREG0
	LDA XREG
	STA XREG0
	LDA YREG
	STA YREG0
	LDA PREG
	STA PREG0
	STA HALT,X
	JSR RESTORE	RESTORE 6502 REGISTOR
	CLI		ENABLE 6502 INTERRUPTS
GO	JSR XXXX	RUN THE 6502 SUBROUTINE
	SEI		DISABLE 6502 INTERRUPTS
	JSR SAVE	STORE 6502 REGISTORS
	LDX AP10
	STA RAMDF,X
	LDA AREG0
	STA AREG
	LDA XREG0
	STA XREG
	LDA YREG0	
	STA YREG
	LDA PREG0
	STA PREG
	JMP START
XXXX	RTS
ZZZZ	.EN
