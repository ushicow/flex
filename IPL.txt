*
* IPL FOR THE MILL FLEX
* 6502 CODE
*
	.OR $7700
	.TF IPL

MILL	.EQ $2		MILL SLOT
RWTS	.EQ $77B5	APPLE DOS RWTB
IOB	.EQ $77E8	APPLE DOS IOB
DSLOT	.EQ IOB+1
CURDRV	.EQ IOB+2
VOLREG	.EQ IOB+3
TRKREG	.EQ IOB+4
SECREG	.EQ IOB+5
SECBUF	.EQ IOB+8
NUMBUF	.EQ IOB+$B
COMREG	.EQ IOB+$C
RETCODE	.EQ IOB+$D
LSTSLOT	.EQ IOB+$F
LSTDRV	.EQ IOB+$10
MTROFF	.EQ $C088	DRIVE MOTER OFF
MONITR	.EQ $53EF	6809 ADDRESS $D3EF

BIOS	.EQ $6000	BIOS START ADDRESS

BOOT2	JSR BOOTX
	STX DSLOT	SET DISK I/F SLOT
	STX LSTSLOT	SET DISK I/F SLOT
	LDA #1		READ COMMAND
	STA CURDRV
	STA LSTDRV
	STA COMREG
	LDA #0
	STA VOLREG
	STA TRKREG
	STA SECBUF	SECTOR BUFFER ADDRESS IS $xx00

	LDA #$A		SECTOR A: MILLBIOS
	STA SECREG
	LDA #BIOS/256	  READ TO BIOS START ADDRESS
	JSR RDSEC
	
	LDA #$B		SECTOR B: DRIVER
	STA SECREG
	LDA #$5E	  READ TO $5E00 (6809 $DE00)
	JSR RDSEC

	LDA #$C		SECTOR C: CONSOLE
	STA SECREG
	LDA #$53	  READ TO $5300 (6809 $D300)
	JSR RDSEC

	LDA #$D		SECTOR D: FLEXLOAD
	STA SECREG
	LDA #BIOS/256+1	  READ TO BIOS + $100
	JSR RDSEC

	LDA #$E		SECTOR E: TESTUTIL
	STA SECREG
	LDA #BIOS/256+2	  READ TO BIOS + $200
	JSR RDSEC

	LDA #$F		SECTOR F:
	STA SECREG
	LDA #BIOS/256+3	  READ TO BIOS + $300
	JSR RDSEC

	LDA #$1		TRACK 1 / SECTOR 0 FLEXLOAD
	STA TRKREG
	LDA #$0
	STA SECREG
	LDA #$41	  READ TO $4100 (6809 $C100)
	JSR RDSEC

	JMP BIOS

RDSEC	STA SECBUF+1	READ A SECTOR, AREG IS HIGH MEM ADDRESS
	LDA #IOB/256	
	LDY #IOB
	JMP RWTS

	.OR $85D
	.TF IPL2
BOOTX	JSR $FC58	F8ROM:HOME
	LDY #$90
NSLOT	LDA $C000,Y
	CMP #$C0	CHECK MILL CARD
	BEQ FOUND
	TYA
	CLC
	ADC #$10
	TAY
	BNE NSLOT

	LDX DSLOT
	LDA MTROFF,X	DRIVE MOTER OFF
	LDY #$FF	MILL NOT FOUND
.1	INY
	LDA NOMILL,Y
	ORA #$80
	JSR $FDF0	F8ROM:COUT1
	ASL
	BNE .1
	BRK

FOUND	TYA
	AND #$70
	STA MILL	STORE MILL SLOT NUMBER X 16
	LSR		A/2
	LSR		A/4
	LSR		A/8
	LSR		A/16
	CLC
	ADC #'0+$80
	STA SLOTN

	LDY #$FF
.2	INY
	LDA MES,Y
	ORA #$80
	JSR $FDF0	F8ROM:COUT1
	ASL
	BNE .2

	RTS

MES	.AS "THE MILL SLOT #"
SLOTN	.DA #0
OS9	.DA $8A8D
	.DA #7		BELL
	.AS "OS9 DAUGHTER CARD SWITCH MUST BE SET OFF"
	.DA #0
NOMILL	.DA #7		BELL
	.AS "THE MILL NOT FOUND"
	.DA #0
	.EN
