*
* IPL.ASM
*
* IPL FOR THE MILL FLEX
* 6502 CODE
*
	.MA PRINT	>PRINT MESSAGE
	LDY #$FF
:1	INY
	LDA ]1,Y
	ORA #$80
	JSR COUT1
	ASL
	BNE :1
	.EM

	.OR $7700
	.TF IPL
MFLIP	.EQ 0		6502 ADDRESS
	.IN APPLE.EQU
*
BOOT2	STX DSLOT	SET DISK I/F SLOT
	STX LSTSLOT	SET DISK I/F SLOT
	JSR SEARCH	SEARCH THE MILL
	LDA #1		
	STA CURDRV	 DRIVE 1
	STA LSTDRV	 DRIVE 1
	STA COMREG	 READ COMMAND
	LDA #0
	STA VOLREG	 ANY VOLUME
	STA TRKREG	 TRACK 0
	STA SECBUF	 SECTOR BUFFER ADDRESS IS $xx00

	LDA #$A		SECTOR A: MILLBIOS
	STA SECREG
	LDA #BIOS/256	  READ TO BIOS START ADDRESS
	JSR RDSEC
	
	LDA #$B		SECTOR B: DRIVER
	STA SECREG
	LDA #$5E	  READ TO $5E00 (6809 $DE00)
	JSR RDSEC

	LDA #$C		SECTOR C: CONSOLE
	STA SECREG
	LDA #$53	  READ TO $5300 (6809 $D300)
	JSR RDSEC

	LDA #$D		SECTOR D: FLEXLOAD
	STA SECREG
	LDA #BIOS/256+1	  READ TO $6100 (6809 $E100)
	JSR RDSEC

	LDA #$E		SECTOR E: TESTUTIL
	STA SECREG
	LDA #BIOS/256+2	  READ TO $6200 (6809 $E200)
	JSR RDSEC

	LDA #$F		SECTOR F: OS9ERROR
	STA SECREG
	LDA #BIOS/256+3	  READ TO $6300 (6809 $E300)
	JSR RDSEC

	LDA #$1		TRACK 1 / SECTOR 0 FLEXLOAD
	STA TRKREG
	LDA #$0
	STA SECREG
	LDA #$41	  READ TO $4100 (6809 $C100)
	JSR RDSEC

	LDA #$3B	6809 RTI command
	STA $7FF0
	LDA #$39	6809 RTS command
	STA $7FF1
	LDA #$FF
	STA $7FF2 	SW3
	STA $7FF4	SW2
	STA $7FF6	FIRQ
	STA $7FF8	IRQ
	STA $7FFA	SW1
	STA $7FFC	NMI
	LDA #$F0
	STA $7FF3
	STA $7FF5
	STA $7FF7
	STA $7FF9
	STA $7FFB
	STA $7FFD
	LDA #$3F	SET MEMEND to $3FFF
	STA MEMEND
	LDA #$FF
	STA MEMEND+1
	JMP BIOS

RDSEC	STA SECBUF+1	READ A SECTOR, AREG IS HIGH MEM ADDRESS
	LDA #IOB/256	
	LDY #IOB
	JMP RWTS

ZZA	.EQ *
ZZAFREE	.EQ RWTS-ZZA	CHECK MEMORY OVER RUN

	.OR $85D	USE APPEND BUG PATCHES AREA
	.TF IPL2
*
* SERACH THE MILL
*
SEARCH	LDA #$C1	SET 6809 RESET VECTOR FOR FLEXLOAD
	STA $7FFE
	LDA #$00
	STA $7FFF
	STA $8FFF	FOR OS-9 SETTING RESET VECTOR
	LDA #$D3
	STA $8FFE

	JSR HOME
	LDY #$90	SLOT #1
NSLOT	LDA $C000,Y
	CMP #$C0	CHECK MILL CARD
	BEQ FOUND
	TYA		Y=Y+$10
	CLC
	ADC #$10
	TAY
	BNE NSLOT	NEXT SLOT
*
	LDX DSLOT	THE MILL NOT FOUND
	LDA MTROFF,X	DRIVE MOTER OFF
	>PRINT NOMILL
	BRK

FOUND	TYA
	AND #$70
	STA MILL	STORE MILL SLOT NUMBER X 16
	LSR		A/2
	LSR		A/4
	LSR		A/8
	LSR		A/16
	CLC
	ADC #'0+$80
	STA SLOTN
	>PRINT MES
	RTS

CR	.EQ $8D
BELL	.EQ $7
MES	.AS "THE MILL SLOT #"
SLOTN	.DA #'0
	.DA #CR
	.DA #0
NOMILL	.DA #BELL
	.AS "THE MILL NOT FOUND"
	.DA #0

ZZB	.EQ *
ZZBFREE	.EQ $8FE-ZZB

	.EN
