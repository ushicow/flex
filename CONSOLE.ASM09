*
* CONSOLE I/O DRIVER PACKAGE 
*
* THIS VERSION IS FOR IBS AP10, APPLE II 6809 CARD
* NOV.20,2021 NANJA.INFO
*
	.IN APPLE.EQU
*
WSTART	.EQ $CD03	FLEX WARM START
*
	.OR $D370
	.TF CONSOLE.5370
*
* TERMINAL INITIALZE ROUTINE 
*
INIT	LDX #$BFFF	FOR AP10
	LDA FLIP
	BEQ SETMEND
	LDX #$3FFF	FOR THE MILL
SETMEND	STX $CC2B	SET MEMEND
	RTS
*
* TERMINAL INPUT CHAR. ROUTINE - NO ECHO
*
INNECH	EXG D,D		IDLE
	LDA KEYLOC
	BEQ INNECH
	CLR KEYLOC
RET	RTS
*
* TERMINAL INPUT CHAR. ROUTINE - W/ ECHO
*
INPUT	LDA #RDKEY/256	SET APPLE II ROM ROUTINE ADDRESS
	STA A2SUB
	LDA #RDKEY
	STA A2SUB+1
	LDA #F.6502	CALL APPLE II ROUTINE
	STA FLAG
I0	EXG D,D		IDLE
	TST FLAG	WAIT APPLE II ROUTINE
	BNE I0
	LDA AREG	GET THE CHARACTER
	ANDA #$7F	STRIP PARITY
*
* TERMINAL OUTPUT CHARACTER ROUTINE
*
OUTPUT	STA TVLOC
O1	EXG D,D		IDLE
	TST TVLOC
	BNE O1		NOT READY YET
	RTS
*
* TERMINAL STATUS CHECK (CHECK FOR CHARACTER HIT)
*
STATUS	TST KEYLOC
	RTS
*
* IRQ INTERRUPT HANDLER ROUTINE
*
IHND	RTI
*
* WASTE AREA FOR SWI AND IRQ
*
WASTE	.DA 0
*
* BREAK TO APPLE II MONITOR
*
MON	LDA #F.STOP
	STA FLAG
WAIT	EXG D,D
	TST FLAG
	BNE WAIT
	JMP WSTART
ZZZZ	.EQ $D3E5-*
	.BS ZZZZ	FILL UP
*
* I/O ROUTINE VECTOR TABLE
*
*	.OR $D3E5	TABLE STARTS AT $D3E5
*	.TF CONSOLE.53E5
INCHNE	.DA INNECH	INPUT CHAR - NO ECHO
IHNDLR	.DA IHND	IRQ INTERRUPT HANDLER
SWIVEC	.DA WASTE	SWI3 VECTOR LOCATION
IRQVEC	.DA WASTE	IRQ VECTOR LOCATION
TMOFF	.DA RET		TIMER OFF ROUTINE
TMON	.DA RET		TIMER ON ROUTINE
TMINT	.DA RET		TIMER INITIALIZE ROUTINE
MONITR	.DA MON		MONITOR ENTRY ADDRESS
TINIT	.DA INIT	TERMINAL INITIALIZATION
STAT	.DA STATUS	CHECK TERMINAL STATUS
OUTCH	.DA OUTPUT	TERMINAL CHAR OUTPUT
INCH	.DA INPUT	TERMINAL CHAR INPUT
	.EN
