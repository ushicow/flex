*
* CONSOLE I/O DRIVER PACKAGE 
*
* THIS VERSION IS FOR IBS AP10, APPLE II 6809 CARD
* NOV.20,2021 NANJA.INFO
*
	.IN APPLE.EQU

WSTART	.EQ $CD03	FLEX WARM START

	.OR $D370
	.TF CONSOLE.5370
*
* TERMINAL INITIALZE ROUTINE 
*
INIT	RTS
*
* TERMINAL INPUT CHAR. ROUTINE - NO ECHO
*
INNECH	EXG D,D		IDLE
	LDA KEYLOC
	BEQ INNECH
	CLR KEYLOC
RET	RTS
*
* TERMINAL INPUT CHAR. ROUTINE - W/ ECHO
*
INPUT	LDA #RDKEY/256	SET APPLE II ROM ROUTINE ADDRESS
	STA A2SUB
	LDA #RDKEY
	STA A2SUB+1
	LDA #F.6502	CALL APPLE II ROUTINE
	STA FLAG
I1	EXG D,D		IDLE
	TST FLAG	WAIT APPLE II ROUTINE
	BNE I1
	LDA AREG	GET THE CHARACTER
	ANDA #$7F	STRIP PARITY
*
* TERMINAL OUTPUT CHARACTER ROUTINE
*
OUTPUT	EXG D,D		IDLE
	TST TVLOC
	BNE OUTPUT	NOT READY YET
	STA TVLOC	OUTPUT A CHARACTER
	RTS
*
* TERMINAL STATUS CHECK (CHECK FOR CHARACTER HIT)
*
STATUS	TST KEYLOC
	RTS
*
* IRQ INTERRUPT HANDLER ROUTINE
*
IHND	RTI
*
* BREAK TO APPLE II MONITOR
*
MON	LDA #F.STOP
	STA FLAG
WAIT	EXG D,D
	TST FLAG
	BNE WAIT
	JMP WSTART
*
* CONSOLE I/O DRIVER VECTOR TABLE
*
	.OR $D3E5	TABLE STARTS AT $D3E5
	.TF CONSOLE.53E5
INCHNE	.DA INNECH	INPUT CHARACTER W/O ECHO
IHNDLR	.DA IHND	IRQ INTERRUPT HANDELER
SWIVEC	.DA $FFF2	SWI3 VECTOR LOCATION
IRQVEC	.DA $FFF6	IRQ VECTOR LOCATION
TMOFF	.DA RET		TIMER OFF ROUTINE
TMON	.DA RET		TIMER ON ROUTINE
TMINT	.DA RET		TIMER INITIALIZATION
MONITR	.DA MON		MONITOR ENTRY ADDRESS
TINIT	.DA INIT	TERMINAL INITIALIZATION
STAT	.DA STATUS	CHECK TERMINAL STATUS
OUTCH	.DA OUTPUT	OUTPUT CHARACTOR
INCH	.DA INPUT	INPUT CHARACTOR W/ ECHO
	.EN
